apply plugin: 'com.android.application'

android {
    compileSdkVersion 35
    defaultConfig {
        namespace "com.aefyr.sai"
        applicationId "com.aefyr.sai"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 60
        versionName "4.5"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        normal {
            dimension "version"
            buildConfigField "int", "DEFAULT_THEME", "0"
            buildConfigField "int", "DEFAULT_DARK_THEME", "1"
            buildConfigField "boolean", "HIDE_DONATE_BUTTON", "false"
        }

        fdroid {
            dimension "version"
            applicationId "com.aefyr.sai.fdroid"
            buildConfigField "int", "DEFAULT_THEME", "6"
            buildConfigField "int", "DEFAULT_DARK_THEME", "11"
            buildConfigField "boolean", "HIDE_DONATE_BUTTON", "false"
        }
    }

    compileOptions {
        sourceCompatibility = '21'
        targetCompatibility = '21'
    }

    buildFeatures {
        buildConfig = true
    }
    namespace 'com.aefyr.sai'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.8.7'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.tomergoldst.android:tooltips:1.1.1'
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation('com.github.aefyr.flexfilter:flexfilter:0.9-alpha') {
        exclude group: 'com.google.android', module: 'flexbox'
    }
//    implementation files('libs/pseudoapksigner-1.8.jar')
//    implementation 'com.github.aefyr:pseudoapksigner:1.8'
    implementation 'io.github.solrudev:pseudoapksigner:1.8'

    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    //Google services
    normalImplementation 'com.google.firebase:firebase-analytics:22.3.0'
    normalImplementation 'com.google.firebase:firebase-crashlytics:19.4.1'
    normalImplementation 'com.android.billingclient:billing:7.1.1'

    //Shizuku/Sui
    def shizuku_version = '12.2.0'
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains("Normal")){
    println "Google Play build detected, applying Google Services and Crashlytics plugins"
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}


